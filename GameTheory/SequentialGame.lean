
import GameTheory.StrategicGame

/-

A sequential game consisting of:
- a set I of players
- a set X of game states
- a set A of possible actions (assumed the same for all players)
- a map 'turn' that tells whose turn it is from a given state
- a map 'move' that takes an action profile and updates the state
- each player has a preference on state trajectories

Some formalizations assume players take the state history as input for their decision.
For simplicity let's just assume the state variable already contains that information.

-/

-- The sequential game struct just comes with a mapping from action profiles to state updates.

variable {I A X: Type} [DecidableEq I]

class SeqGameStruct (I A X: Type) where
  move: (I → A) → X → X

-- This is enough to define the one-step update from an initial state along with the full state sequence.

def SeqGameStruct.step (G: SeqGameStruct I A X) (π: I → X → A) (ε: X): X :=
  G.move (fun p => π p ε) ε

def SeqGameStruct.run (G: SeqGameStruct I A X) (π: I → X → A) (ε: X): Nat → X :=
  fun n => match n with
  | Nat.zero => ε
  | Nat.succ prev => G.step π (G.run π ε prev)

-- A full sequential game comes with a preference relation on trajectories.

class SeqGame (I A X: Type) extends SeqGameStruct I A X where
  pref: I → Relation (Nat → X)


-- Given a sequential game and an initial state, there is a corresponding outcome game where the outcomes are trajectories.

def SeqGame.toOutcomeGame (G: SeqGame I A X) (ε: X): OutcomeGame I (X → A) (Nat → X) := {
  play := fun π => G.run π ε
  pref := G.pref
}

-- A subgame perfect equilibrium is a strategy profile in which every subgame is a Nash equilibrium.

def SeqGame.subgame_perfect_equilibrium (G: SeqGame I A X) (π: I → X → A): Prop :=
  ∀ ε, (G.toOutcomeGame ε).toGame.nash_eq π

-- A sequential utility game in which every state comes with a utility for each players.
-- (This is simpler than assigning utilities to transitions, in principle I think you can pack these into the state.)

class SeqUtilityGame (I A X U: Type) extends SeqGameStruct I A X where
  uvalue: I → X → U
  prefer: Relation U

-- Next we will assume we have a map σ that "sums up" the utilities along trajectories.
-- In this case, the h-value (history value) of a trajectory is the sum of the sequence.
-- The π-value is the sum of the trajectory generated by π.

def hvalue (G: SeqUtilityGame I A X U) (σ: (Nat → U) → U) (h: Nat → X) (p: I): U :=
  σ (fun t => G.uvalue p (h t))

def πvalue (G: SeqUtilityGame I A X U) (σ: (Nat → U) → U) (π: I → X → A) (ε: X) (p: I): U :=
  let h := G.run π ε
  hvalue G σ h p

-- Given a sequential utility game, an initial state ε, and a utility summing function σ,
-- there is a corresponding utility game.

@[simp]
def SeqUtilityGame.toUtilityGame (G: SeqUtilityGame I A X U) (σ: (Nat → U) → U) (ε: X): UtilityGame I (X → A) U := {
  play := fun π =>
    let h := G.run π ε
    hvalue G σ h
  pref := G.prefer
}


/-

Dynamic game, a nice generalization.

-/

class DynamicGame (I S X T: Type) [Zero T] where
  play: (I → S) → T → X → X
  pref: I → Relation (T → X)

variable {S T: Type} [Zero T]

-- Given a dynamic game, a strategy profile, and an initial state,
-- we can "run" the game and get the corresponding state trajectory.

def DynamicGame.run (G: DynamicGame I S X T) (π: I → S) (x₀: X): T → X :=
  fun t => G.play π t x₀

-- Xvery dynamic game with a fixed initial state is an outcome game where outcomes are state trajectors T → X.

instance DynamicGame.toOutcomeGame (G: DynamicGame I S X T) (x₀: X): OutcomeGame I S (T → X) := {
  play := fun π => G.run π x₀
  pref := G.pref
}

def DynamicGame.subgame_perfect_equilibrium (G: DynamicGame I S X T) (π: I → S): Prop :=
  ∀ x₀, (G.toOutcomeGame x₀).toGame.nash_eq π

-- Every sequential game is a dynamic game with T = ℕ.

example (G: SeqGame I A X): DynamicGame I (X → A) X Nat := {
  play := fun π t x₀ => G.run π x₀ t
  pref := G.pref
}

/-

Bellman optimality principle attempt:

Suppose we have a sequential utility game,
equipped with σ (sum-like) and α (addition-like)
such that for any sequence u0, u1, u2, ... of utilities:

σ(u0, u1, u2, ...) = α(u0, σ(u1, u2, ...))

(For standard discounted weighting take α(u, v) = u + βv, for nondiscounted take α(u, v) = u + v.)

Fix a state s, player p, let π0, π1 be profiles.
Let Up(s) be the utility player p associates to state s.
Let s0, s1 be the result of play under π0, π1 after one step starting from s.
Let Vp(s0, π0) and Vp(s1, π1) be the longterm value of states s0 and s1, played under π0 and π1 respectively.

Theorem: if Up(s) + Vp(s0, π0) ≤ Up(s) + Vp(s1, π0) then p prefers π0 ≤ π1 in the underlying normal-form game started from s.

-/

def tail {U: Type} (u: Nat → U): Nat → U :=
  fun t => u (t + 1)

def SeqUtilityGame.NormalForm (G: SeqUtilityGame I A X U) (σ: (Nat → U) → U) (ε: X): Game I (X → A) :=
  (G.toUtilityGame σ ε).toOutcomeGame.toGame

example (G: SeqUtilityGame I A X U)
  (σ: (Nat → U) → U)
  (α: U → U → U)
  (h0: ∀ u: Nat → U, α (u 0) (σ (tail u)) = σ u)
  (ε: X) (p: I) (π0 π1: I → X → A)
  (h1: G.prefer (α (G.uvalue p ε) (πvalue G σ π0 (G.move (flip π0 ε) ε) p)) (α (G.uvalue p ε) (πvalue G σ π1 (G.move (flip π1 ε) ε) p))):
  (G.NormalForm σ ε).pref p π0 π1 := by
  simp_all [SeqUtilityGame.NormalForm, SeqUtilityGame.toUtilityGame, UtilityGame.toOutcomeGame, OutcomeGame.toGame]
  simp_all [πvalue, hvalue]
  rw [←h0]
  rw (config := {occs := .pos [2]}) [←h0]
  simp_all [SeqGameStruct.run]
  -- obnoxious lemma
  have {X Y: Type} {a b c d x: X} {f: X → X → Y} {g: Y → Y → Prop} (h1: g (f x a) (f x b)) (h2: a = c) (h3: b = d): g (f x c) (f x d) := by
    rw [←h2, ←h3]
    exact h1
  apply this h1 <;> (
    congr
    ext t
    simp [tail, SeqGameStruct.run]
    congr
    induction t with
    | zero => simp [SeqGameStruct.run]; rfl
    | succ previous ih => simp [SeqGameStruct.run, ih]
  )

-- TODO: definition of a valuation and something interesting about them?

-- Let G be a sequential game and fix a player p.
-- An arbitrary function v: X → U along with a preference on U is called a valuation for player p
-- if maximizing v always leads to preferable trajectories.

-- Note: this doesn't depend on the utility at ε itself?
def valuation (G: SeqGame I A X) (p: I) (Vp: X → U) (upref: Relation U): Prop :=
  ∀ π0 π1 ε, G.pref p (G.run π0 ε) (G.run π1 ε) ↔ upref (Vp (G.step π0 ε)) (Vp (G.step π1 ε))
